# 5 補足

<p></p>

 

## 5.1 ハンズオン環境のクリーンアップ

下記の手順に従い、ハンズオンで作成・設定した内容を削除します。

### <u>クリーンアップ手順</u>

**□ C1.** QSECOFR同等の高権限ユーザーでサインオン。

**□ C2.** ネットワーク共有を作成した場合はこれを削除。

**□ C3.** IWSで作成したサーバーが存在する場合はこれを削除。

**□ C4.** 次のコマンドでハンズオン用に作成したディレクトリー「/home/WEBSXX」を削除。

```
> RMVDIR DIR('/home/WEBSXX') SUBTREE(*ALL)  
```

**□ C5.**  WRKSPLFコマンドで生成したスプールファイルを表示し、すべてのスプールファイルの「OPT」に「4=削除」を指定して削除。

**□ C6.** ライブラリー「WEBSxxLIB」を削除。

**□ C7.** コマンド「WRKOBJOWN」を実行し、下記を確認。

* ライブラリー「QRPLOBJ」および「QUSRSYS」、「QTEMP」以外のライブラリーにオブジェクトが存在しない
* IFSにオブジェクト(ファイルやディレクトリー)が存在しない

**□ C8.** ハンズオン用のユーザープロファイルを削除。

<br>

## 5.2 ソースコード

英小文字を含むソースがSEUで正常に表示されない (Ctrl+F3を押しても英小文字が化ける) 場合は下記を試します。

* CHGJOB CCSID(5026)を実行してからソースを閲覧
* ACSのホスト・コード・ページを5035または1399に設定してソースを閲覧

<br>

### 5.2.1 ILE-RPGプログラム：WEBSxxLIB/WSCGI

「4.3 ③ CGI (ILE-RPG)」で取り上げた、ILE-RPGで記述されたCGIプログラムでWebサービスを実装した例です。

```js
0001 H OPTION(*SRCSTMT:*NOUNREF:*NODEBUGIO)
0002 H DFTACTGRP(*NO) ACTGRP(*CALLER)
0003 H CCSIDCVT(*LIST)
0004  ************************************************************
0005  * ファイル定義
0006  ************************************************************
0007  *   登録者一覧 - KEY:REGNO 登録番号
0008 FPERSON    IF   E           K DISK
0009  *   登録者一覧 - KEY:KNNAME 姓名（読み）
0010 FPERSONL1  IF   E           K DISK    RENAME(PERSONR:PERSONLR)
0011  *
0012  ************************************************************
0013  * プロトタイプ宣言
0014  ************************************************************
0015  *
0016 D getenv          PR              *   EXTPROC('getenv')
0017 D   envvar                        *   VALUE OPTIONS(*STRING)
0018  *
0019 D puts            PR            10I 0 EXTPROC('puts')
0020 D                                 *   VALUE
0021 D read            PR            10I 0 EXTPROC('read')
0022 D   file_descriptor...
0023 D                               10I 0 VALUE
0024 D   buf                           *   VALUE
0025 D   nbyte                       10I 0 VALUE
0026  *
0027 D fflush          PR            10I 0 EXTPROC('fflush')
0028 D                                 *   VALUE
0029  * 数値→文字変換
0030 Dn2c              PR            20    VARYING
0031 D  numeric                      10I 0 VALUE
0032  *
0033  * 二重引用符のエスケープ
0034 DescDQ            PR          1024    VARYING
0035 D  inStr                      1024    VALUE
0036  ************************************************************
0037  * 変数の宣言
0038  *
0039 D req_method      S           1024    VARYING
0040 D query_str       S                   LIKE(req_method)
0041  * "#define STDIN_FILENO  0" from QSYSINC/H.UNISTD
0042 D STDIN_FILENO    C                   0
0043 D stdin_utf8      S           1024    CCSID(1208)
0044 D stdin_data      S           1024    CCSID(1399)
0045 D stdin_len       S             10I 0
0046 D stdout_data     S        1000000
0047 D start_pos       S              5P 0
0048 D I               S              5P 0
0049 D DQpos           S              5P 0 DIM(4)
0050  *
0051 D rcd_cnt         S              5P 0
0052 D key_regno       S              5S 0
0053 D key_kana        S             20
0054 D json_data       S        1000000    VARYING
0055 D rc              S             10I 0
0056 D newLine         C                   X'15'
0057 D null            C                   X'00'
0058  ************************************************************
0059
0060   // 環境変数 REQUEST_METHOD の取得
0061   req_method = %STR(getenv('REQUEST_METHOD'));
0062
0063   SELECT;
0064     WHEN req_method = 'GET';
0065       EXSR #GETPERSON;
0066     WHEN req_method = 'POST';
0067       EXSR #POSTPERSON;
0068     OTHER;
0069       EXSR #HTTP405;
0070   ENDSL;
0071
0072   // *INLR = *ON;
0073   RETURN;
0074
0075  ************************************************************
0076  * サブルーチン
0077  **************************************
0078  * 登録番号をキーとした 1 レコード検索
0079
0080   BEGSR #GETPERSON;
0081
0082     MONITOR;
0083       query_str = %STR(getenv('QUERY_STRING'));
0084       key_regno = %DEC(
0085                   %SUBST(query_str : %SCAN('regNo=' : query_str) + 6) :
0086                   5 : 0);
0087     ON-ERROR;
0088       EXSR #HTTP500;
0089       LEAVESR;
0090     ENDMON;
0091
0092     CHAIN(E) key_regno PERSONR;
0093
0094     IF NOT %FOUND;
0095       EXSR #HTTP404;
0096       LEAVESR;
0097     ENDIF;
0098
0099     json_data = '{"single_rcd":';
0100     EXSR #APPENDDATA;
0101     json_data += '}';
0102     EXSR #HTTP200;
0103
0104   ENDSR;
0105  **************************************
0106  * 名前（カナ）を前方一致の部分キーとした複数レコード検索
0107
0108   BEGSR #POSTPERSON;
0109
0110     stdin_len = read(STDIN_FILENO : %ADDR(stdin_utf8) : 1024);
0111     IF stdin_len = 0;
0112       EXSR #HTTP404;
0113       LEAVESR;
0114     ENDIF;
0115     // UTF-8 から EBCDIC 1399 への変換
0116     stdin_data = %SUBST(stdin_utf8 : 1 : stdin_len);
0117
0118     // JSON からキー値の取り出し
0119     MONITOR;
0120       // 「 " 」 (X'7F') をスキャン
0121       start_pos = 1;
0122       FOR I = 1 TO 4;
0123         DQpos(I) = %SCAN(X'7F' : stdin_data : start_pos);
0124         IF DQpos(I) = 0;
0125           LEAVE;
0126         ENDIF;
0127         start_pos = DQpos(I) + 1;
0128       ENDFOR;
0129       I -= 1; // FOR ループを抜けた時に I は終値 + 1 になるため補正
0130       IF (I < 4) OR (DQpos(4) = 0);
0131         EXSR #HTTP404;
0132         LEAVESR;
0133       ENDIF;
0134       IF %SUBST(stdin_data : DQpos(1) + 1 : DQpos(2) - DQpos(1) - 1) <>
0135          'kanaName';
0136         EXSR #HTTP404;
0137         LEAVESR;
0138       ENDIF;
0139       key_kana =
0140          %SUBST(stdin_data : DQpos(3) + 1 : DQpos(4) - DQpos(3) - 1);
0141     ON-ERROR;
0142       EXSR #HTTP500;
0143       LEAVESR;
0144     ENDMON;
0145
0146     // 前方一致キーで DB を読み込み
0147     json_data = '';
0148     rcd_cnt = 1;
0149     SETLL(E) key_kana PERSONLR;
0150
0151     IF %FOUND(PERSONL1);
0152       DOW (1 = 1);
0153         READ PERSONL1;
0154         IF %EOF(PERSONL1);
0155           LEAVE;
0156         ENDIF;
0157         IF key_kana <> %SUBST(KNNAME : 1 : %LEN(%TRIMR(key_kana)));
0158           LEAVE;
0159         ENDIF;
0160         IF rcd_cnt > 1;
0161           json_data += ',';
0162         ENDIF;
0163         EXSR #APPENDDATA;
0164         rcd_cnt += 1;
0165         IF rcd_cnt > 1000;
0166           LEAVE;
0167         ENDIF;
0168       ENDDO;
0169     ENDIF;
0170
0171     rcd_cnt -= 1;
0172     IF rcd_cnt = 0;
0173       EXSR #HTTP404;
0174       LEAVESR;
0175     ENDIF;
0176
0177     json_data = '{"rcd_cnt":' + %CHAR(rcd_cnt) +
0178                 ',"multi_rcds":[' + json_data + ']}';
0179     EXSR #HTTP200;
0180
0181   ENDSR;
0182  *
0183  **************************************
0184   BEGSR #APPENDDATA;
0185     json_data += '{"registNumber":'   + n2c(REGNO)    + ',' +
0186                   '"name_kanji":"'    + escDQ(KJNAME) + '",' +
0187                   '"name_kana":"'     + escDQ(KNNAME) + '",' +
0188                   '"gender":"'        + escDQ(GENDER) + '",' +
0189                   '"tel_primary":"'   + escDQ(TEL)    + '",' +
0190                   '"tel_secondary":"' + escDQ(MOBILE) + '",' +
0191                   '"postalCode":"'    + escDQ(POST)   + '",' +
0192                   '"prefecture":"'    + escDQ(PREF)   + '",' +
0193                   '"address_1":"'     + escDQ(ADDR1)  + '",' +
0194                   '"address_2":"'     + escDQ(ADDR2)  + '",' +
0195                   '"address_3":"'     + escDQ(ADDR3)  + '",' +
0196                   '"dateOfBirth":'    + n2c(BIRTHD) + '}';
0197   ENDSR;
0198  **************************************
0199  *
0200   BEGSR #HTTP200;
0201     stdout_data = 'Status: 200 OK' + newLine +
0202                   'Content-type: text/json' + newLine +
0203                   newLine +
0204                   json_data + null;
0205     EXSR #STDOUT;
0206   ENDSR;
0207  **************************************
0208  *
0209   BEGSR #HTTP404;
0210     stdout_data = 'Status: 404 Not found' + newLine +
0211                   'Content-type: text/plain' + newLine +
0212                   newLine +
0213                   '404 Not found.' + null;
0214     EXSR #STDOUT;
0215   ENDSR;
0216  **************************************
0217  *
0218   BEGSR #HTTP405;
0219     stdout_data = 'Status: 405 Method Not Allowed' + newLine +
0220                   'Content-type: text/plain' + newLine +
0221                   newLine +
0222                   '405 Method Not Allowed.' + null;
0223     EXSR #STDOUT;
0224   ENDSR;
0225  **************************************
0226  *
0227   BEGSR #HTTP500;
0228     stdout_data = 'Status: 500 Internal Server Error' + newLine +
0229                   'Content-type: text/plain' + newLine +
0230                   newLine +
0231                   '500 Internal Server Error.' + null;
0232     EXSR #STDOUT;
0233   ENDSR;
0234  **************************************
0235  *
0236   BEGSR #STDOUT;
0237     rc = puts(%ADDR(stdout_data));
0238     rc = fflush(*NULL);
0239   ENDSR;
0240  ************************************************************
0241  * Subprocedures
0242  ************************************************************
0243  * 数値→文字変換
0244 Pn2c              B
0245 Dn2c              PI            20    VARYING
0246 D  numeric                      10I 0 VALUE
0247  *
0248 C                   RETURN    %TRIML(%EDITC(numeric : 'P'))
0249  *
0250 Pn2c              E
0251  **************************************
0252  * 二重引用符のエスケープ
0253 PescDQ            B
0254 DescDQ            PI          1024    VARYING
0255 D  inStr                      1024    VALUE
0256  *
0257 D inStrLen        S              5P 0
0258 D inDBCS          S               N
0259 D aChar           S              1
0260  * Loop counter
0261 D I               S              5P 0
0262  *
0263   inStrLen = %LEN(%TRIMR(inStr));
0264   FOR I = 1 TO inStrLen;
0265     aChar = %SUBST(inStr : I : 1);
0266     IF aChar = X'0E';
0267       inDBCS = *ON;
0268     ELSEIF aChar = X'0F';
0269       inDBCS = *OFF;
0270     ENDIF;
0271     IF NOT inDBCS;
0272       IF %SUBST(inStr : I : 1) = '"';
0273         // 二重引用符の前にバックスラッシュ (X'E0') を追加
0274         IF I > 1;
0275           inStr = %SUBST(inStr : I - 1 : 1) + X'E0' +
0276                   %SUBST(inStr : I);
0277         ELSE;
0278           inStr = X'E0' + %SUBST(inStr : I);
0279         ENDIF;
0280       ENDIF;
0281     ENDIF;
0282   ENDFOR;
0283
0284   RETURN %TRIMR(inStr);
0285  *
0286 PescDQ            E
```

* 2行目：パフォーマンス向上のためにACTGRP(*CALLER)を指定。呼び出される度に変数を要初期化。
* 16～28行目： IBMが提供するCGI用のAPI(プロシージャー)ではなく、POSIX標準関数を利用。多くの場合、POSIX関数を利用した方がパフォーマンスが良く、コーディングも容易。これらはバインド・ディレクトリーQSYS/QC2LEに含まれており、ILE-RPGのコンパイル時に暗黙的にバインドされて利用可能になる。16行目のgetenv()はIBM提供のQtmhGetEnv、19行目のputs()はQtmhWrStout、2100行のread()はQtmhRdStinと同様の機能。

?> CGI用APIの詳細はIBM Docsの「CGI API」(https://www.ibm.com/docs/ja/i/7.5?topic=api-cgi-apis )を参照。

* 43～44行目：一部の変数の定義時にCCSIDを指定している。変換がどこで発生しているかを確認するため3行目のH仕様書でCCSIDCVT(*LIST)を指定し、コンパイル・リストに下記のようなCCSID変換発生行を出力。

```
*...+....1....+....2....+....3....+....4....+....5....+....6....+....7
                        C C S I D    変   換                          
          変換前 CCSID      変換後 CCSID       参照                   
         1208              1399                   011600              
         1399              1208                   013400              
         *JOBRUN_MIXED     1208                   013500              
         1399              *JOBRUN_MIXED          014000              
     * * * * *   C C S I D  変   換   の   終   わ   り    * * * * *  
```

* 61行目：環境変数REQUEST_METHODの値を取得。メインルーチンで各メソッドに対応するサブルーチンに分岐。
* 83行目：GET(Webサービス1)の処理を行うため、環境変数QUERY_STRINGを取り出し。
* 99～101行目：検索が正常終了した時に出力するJSONデータを構築。
* 110行目：POST(Webサービス2)の処理を行うため、標準入力を変数「stdin_utf8」に読み込み。
* 118～144行目：JSON形式のデータから検索キー(姓名の読み)を抽出。この例の入力は「{"kanaName" : "ｱｲ"}」のようにシンプルなJSON文字列なので、比較的簡単に検索キーを取り出せるが、より複雑な入力の場合は、SQLや外部ライブラリーを利用した解析を考慮。
* 177～178行目：検索が正常終了した時に出力するJSONデータを構築。
* 185～196行目：データベースから取得したレコードの内容をJSON形式に変換。
* 200～206行目：処理が正常終了した場合の応答を設定。287～233行ではエラーが発生した場合にその内容に応じた応答を設定。
* 237～238行目：クライアントへの出力(応答)を出力。puts()の出力はバッファーされるので、書き出しの最後にfflush()を実行し、ストリームに溜まっているデータを掃き出す。
* 253行目：出力するJSON文字列の値にダブルクォーテーション「”」が存在した場合のエスケープ処理を行うプロシージャー。本来はダブルクォーテーション以外にもスラッシュやバックスラッシュ、改行などの制御文字もエスケープすべきであるが、この例では省略。

<br>

### 5.2.2 ILE-CLプログラム：JKS2KDB

このCLプログラムはJavaに付属するjks形式のキーストアをIBM独自形式のkdbに変換します。「3.3 Ⓒ SQL＋ILE-RPG」の「(参考) QSYS2のHTTP APIでhttpsを利用」に記載のIBM提供SQLスクリプトと同等の処理を行います。実行にはIBM i 7.3以降が必要です。

?> IBM i 7.3と7.4はPTF適用が前提。QSYS2のHTTP関数は「New HTTP functions based in QSYS2」(https://www.ibm.com/support/pages/node/6486889 )を、QSYS2.CERTIFICATE_INFO表関数は「IBM i Services (SQL)」(https://www.ibm.com/support/pages/node/1119123 )を参照。

CRTBNDCLコマンドでコンパイルし、パラメーター無しでプログラムを実行します。

```
> CALL PGM(JKS2KDB)                                        
  The temporary keystore (PKCS12) was successfully created.
  New keystore (KDB) created successfully.                 
  Program finished.                                        
```

処理中に下記のファイルをディレクトリー「/tmp」に作成します。

|ファイル|情報|解説|
|--------|----|---|
|keytool.log|keytool(Qshell)の標準出力|正常時は「別名 XXXX の項目が正常にインポートされました。」が項目数だけ、最後に「インポート・コマンドが完了しました: XX 個の項目が正常にインポートされ、X 個の項目が失敗したか、または取り消されました」と出力される。なお、同じ内容で文字コードがEBCDICの/tmp/keytool.EBCDIC.logも生成される|
|cacerts.csv|キーストアfromJava.KDB内の証明書|キーストア「/home/javaTrustStore/fromJava.KDB」の証明書をQSYS2.CERTIFICATE_INFO 表関数でDB出力し、これをCPYTOIMPFコマンドでCSV変換。キーストアに含まれる証明書の詳細情報を閲覧可能|
|webpage.txt|https経由で取得したWebページ|QSYS2.HTTP_GET関数でhttpsを使用してWebサイトから取得したページの内容|

エラーなどで正常に動作しない場合には、これらファイルの内容を確認すればどこまで処理が行われているかが判別できます。キーストア(KDB)が正常に作成されたら、これらのファイルを削除して構いません。

<br>

```js
0001             PGM
0002
0003             /* Specify the name of a temporary JKS format TRUST STORE      */
0004             DCL        VAR(&TMPPATH) TYPE(*CHAR) LEN(128) +
0005                          VALUE('/home/javaTrustStore/jksExport')
0006             DCL        VAR(&LENTMPPATH) TYPE(*INT) LEN(4)
0007             DCL        VAR(&TMPPASS) TYPE(*CHAR) LEN(128) +
0008                          VALUE('xyz789')
0009             DCL        VAR(&LENTMPPASS) TYPE(*INT) LEN(4)
0010             DCL        VAR(&CIDTMPPASS) TYPE(*INT) LEN(4) VALUE(37)
0011
0012             /* Import Certificate Store (QYKMIMPK, QykmImportKeyStore) API */
0013             /* 1 Certificate store path and file Name                      */
0014             DCL        VAR(&NEWPATH) TYPE(*CHAR) LEN(128) +
0015                          VALUE('/home/javaTrustStore/fromJava.KDB')
0016             /* 2 Length of certificate store path and file Name            */
0017             DCL        VAR(&LENNEWPATH) TYPE(*INT) LEN(4)
0018             /* 3 Format of certificate store path and file Name            */
0019             /* 4 Certificate store password                                */
0020             DCL        VAR(&NEWPASS) TYPE(*CHAR) LEN(128) +
0021                          VALUE('abc123')
0022             /* 5 Length of certificate store password                      */
0023             DCL        VAR(&LENNEWPASS) TYPE(*INT) LEN(4)
0024             /* 6 CCSID of certificate store passwordd                      */
0025             DCL        VAR(&CIDNEWPASS) TYPE(*INT) LEN(4) VALUE(37)
0026             /* 7 Import path and file name                                 */
0027             DCL        VAR(&SRCPATH) TYPE(*CHAR) LEN(128) +
0028                          VALUE('/QOpenSys/QIBM/ProdData/JavaVM/jdk80+
0029                          /64bit/jre/lib/security/cacerts')
0030             /* 8 Length of import path and file name                       */
0031             DCL        VAR(&LENSRCPATH) TYPE(*INT) LEN(4)
0032             /* 9 Format of import path and file name                       */
0033             /* 10 Version of import file                                   */
0034             /* 11 Import file password                                     */
0035             DCL        VAR(&SRCPASS) TYPE(*CHAR) LEN(128) +
0036                          VALUE('changeit')
0037             /* 12 Length of import file password                           */
0038             DCL        VAR(&LENSRCPASS) TYPE(*INT) LEN(4)
0039             /* 13 CCSID of import file password                            */
0040             DCL        VAR(&CIDSRCPASS) TYPE(*INT) LEN(4) VALUE(37)
0041             /* 14 Error code                  Format ERRC0100              */
0042             DCL        VAR(&QUSEC) TYPE(*CHAR) LEN(16)
0043               DCL        VAR(&QUSBPRV) TYPE(*INT) STG(*DEFINED) +
0044                            LEN(4) DEFVAR(&QUSEC 1)
0045               DCL        VAR(&QUSBAVL) TYPE(*INT) STG(*DEFINED) +
0046                            LEN(4) DEFVAR(&QUSEC 5)
0047               DCL        VAR(&QUSEI) TYPE(*CHAR) STG(*DEFINED) LEN(7) +
0048                            DEFVAR(&QUSEC 9)
0049               DCL        VAR(&QUSERVED) TYPE(*CHAR) STG(*DEFINED) +
0050                            LEN(1) DEFVAR(&QUSEC 16)
0051             /* Other variables                                             */
0052             DCL        VAR(&QSHCMD) TYPE(*CHAR) LEN(512)
0053             DCL        VAR(&SQLSTM) TYPE(*CHAR) LEN(512)
0054             DCL        VAR(&LOGCLPGM) TYPE(*CHAR) LEN(10)
0055             DCL        VAR(&JOBCCSID) TYPE(*DEC) LEN(5 0)
0056
0057             /***************************************************************/
0058             /* Setup environment                                           */
0059             /***************************************************************/
0060             RTVJOBA    LOGCLPGM(&LOGCLPGM) CCSID(&JOBCCSID)
0061             CHGJOB     LOGCLPGM(*YES) CCSID(5035)
0062             RMDIR      DIR('/home/javaTrustStore') SUBTREE(*ALL)
0063             MONMSG     MSGID(CPF0000)
0064             MKDIR      DIR('/home/javaTrustStore')
0065
0066             ADDENVVAR  ENVVAR(QIBM_QSH_CMD_ESCAPE_MSG) VALUE(Y) +
0067                          REPLACE(*YES)
0068             ADDENVVAR  ENVVAR(QIBM_QSH_CMD_OUTPUT) +
0069                          VALUE('FILE=/tmp/keytool.EBCDIC.log') +
0070                          CCSID(1208) REPLACE(*YES)
0071             DEL        OBJLNK('/tmp/keytool*')
0072             MONMSG     MSGID(CPF0000)
0073
0074             /***************************************************************/
0075             /* Convert jks to pkcs12                                       */
0076             /***************************************************************/
0077             CHGVAR     VAR(&QSHCMD) VALUE('keytool -importkeystore +
0078                          -srckeystore' |> &SRCPATH |< ' +
0079                          -srcstorepass' |> &SRCPASS |< ' +
0080                          -destkeystore' |> &TMPPATH |< ' +
0081                          -deststorepass' |> &TMPPASS |< ' +
0082                          -srcstoretype JKS -deststoretype PKCS12')
0083
0084             SNDPGMMSG  MSGID(CPF9897) MSGF(QSYS/QCPFMSG) +
0085                          MSGDTA('Step 1 : Converting keystore from +
0086                          JKS to PKCS12...') TOPGMQ(*EXT) +
0087                          MSGTYPE(*STATUS)
0088             QSH        CMD(&QSHCMD)
0089             MONMSG     MSGID(QSH0000) EXEC(DO)
0090             SNDPGMMSG  MSG('!! keytool command failed. Check joblog +
0091                          and file /tmp/keytool.log for detail.')
0092               CPY        OBJ('/tmp/keytool.EBCDIC.log') +
0093                            TOOBJ('/tmp/keytool.log') +
0094                            FROMCCSID(*JOBCCSID) TOCCSID(1208) +
0095                            DTAFMT(*TEXT) REPLACE(*YES)
0096               GOTO       CMDLBL(EXIT)
0097             ENDDO
0098             CPY        OBJ('/tmp/keytool.EBCDIC.log') +
0099                          TOOBJ('/tmp/keytool.log') +
0100                          FROMCCSID(*JOBCCSID) TOCCSID(1208) +
0101                          DTAFMT(*TEXT) REPLACE(*YES)
0102             SNDPGMMSG  MSG('The temporary keystore (PKCS12) was +
0103                          successfully created.')
0104
0105             /***************************************************************/
0106             /* Convert pkcs12 to kdb                                       */
0107             /***************************************************************/
0108             CHGVAR     VAR(&LENSRCPATH) VALUE(%CHECKR(' ' &SRCPATH))
0109             CHGVAR     VAR(&LENSRCPASS) VALUE(%CHECKR(' ' &SRCPASS))
0110             CHGVAR     VAR(&LENTMPPATH) VALUE(%CHECKR(' ' &TMPPATH))
0111             CHGVAR     VAR(&LENTMPPASS) VALUE(%CHECKR(' ' &TMPPASS))
0112             CHGVAR     VAR(&LENNEWPATH) VALUE(%CHECKR(' ' &NEWPATH))
0113             CHGVAR     VAR(&LENNEWPASS) VALUE(%CHECKR(' ' &NEWPASS))
0114             CHGVAR     VAR(&QUSBPRV) VALUE(%LEN(&QUSEC))
0115
0116             SNDPGMMSG  MSGID(CPF9897) MSGF(QSYS/QCPFMSG) +
0117                          MSGDTA('Step 2 : Converting keystore from +
0118                          PKCS12 to KDB...') TOPGMQ(*EXT) +
0119                          MSGTYPE(*STATUS)
0120             CALL       PGM(QSYS/QYKMIMPK) PARM((&NEWPATH) +
0121                          (&LENNEWPATH) ('OBJN0100') (&NEWPASS) +
0122                          (&LENNEWPASS) (&CIDNEWPASS) (&TMPPATH) +
0123                          (&LENTMPPATH) ('OBJN0100') ('*PKCS12V3') +
0124                          (&TMPPASS) (&LENTMPPASS) (&CIDTMPPASS) +
0125                          (&QUSEC))
0126             IF         COND(&QUSBAVL > 0) THEN(DO)
0127               SNDPGMMSG  MSG('!! API QYKMIMPK returned error ' || +
0128                            &QUSEI || '. Check joblog for detail.')
0129               GOTO       CMDLBL(EXIT)
0130             ENDDO
0131             SNDPGMMSG  MSG('New keystore (KDB) created successfully.')
0132
0133             /* List certifications in keystore to database file to CSV */
0134             CHGVAR     VAR(&SQLSTM) VALUE( +
0135              'CREATE OR REPLACE TABLE QTEMP/CACERTS AS (+
0136                 SELECT * FROM TABLE (+
0137                   QSYS2.CERTIFICATE_INFO(+
0138                     CERTIFICATE_STORE_PASSWORD => ''' |< &NEWPASS |< ''', +
0139                     CERTIFICATE_STORE => ''' |< &NEWPATH |<  ''' +
0140                   ) +
0141                 ) +
0142               ) WITH DATA ON REPLACE DELETE ROWS'+
0143             )
0144             RUNSQL     SQL(&SQLSTM) COMMIT(*NC)
0145             CPYTOIMPF  FROMFILE(QTEMP/CACERTS) +
0146                          TOSTMF('/tmp/cacerts.csv') +
0147                          MBROPT(*REPLACE) STMFCCSID(943) +
0148                          RCDDLM(*CRLF) RMVBLANK(*TRAILING) +
0149                          ADDCOLNAM(*SQL)
0150
0151             /***************************************************************/
0152             /* Check https connectivity                                    */
0153             /***************************************************************/
0154             /* Access Web page via https and save the page */
0155             CHGVAR     VAR(&SQLSTM) VALUE( +
0156              'CALL QSYS2.IFS_WRITE_BINARY(+
0157                 PATH_NAME => ''/tmp/webpage.txt'', +
0158                 LINE => (+
0159                   CAST(+
0160                     QSYS2.HTTP_GET(+
0161                       ''https://www.ibm.com/support/pages/sites/default+
0162                        /files/inline-files/xmldoc.xml'',+
0163                       ''{"sslCertificateStoreFile":"' |< &NEWPATH |< '"}''+
0164                     ) AS BLOB(1G) +
0165                   ) +
0166                 ), +
0167                 FILE_CCSID => 1208, +
0168                 OVERWRITE => ''REPLACE'' +
0169               )' +
0170             )
0171             SNDPGMMSG  MSGID(CPF9897) MSGF(QSYS/QCPFMSG) +
0172                          MSGDTA('Step 3 : Checking access to https +
0173                          Web page...') TOPGMQ(*EXT) MSGTYPE(*STATUS)
0174             RUNSQL     SQL(&SQLSTM)
0175
0176 EXIT:       CHGJOB     LOGCLPGM(&LOGCLPGM) CCSID(&JOBCCSID)
0177             RMVENVVAR  ENVVAR(QIBM_QSH_CMD_ESCAPE_MSG)
0178             RMVENVVAR  ENVVAR(QIBM_QSH_CMD_OUTPUT)
0179
0180             SNDPGMMSG  MSGID(CPF9897) MSGF(QSYS/QCPFMSG) +
0181                          MSGDTA('Program finished.') MSGTYPE(*COMP)
0182
0183             ENDPGM
```
